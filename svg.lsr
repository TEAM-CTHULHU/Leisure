#
# Change this to keep attr lists and render items instead of rendering early
# Remove use of <g> from transform
#

require 'maps'

insertFields map = if (null? map) ''
  concat [ (field (firstPair map)), (insertFields (restPairs map)) ]

svgTemplate attrs contents = html (concat [ '<svg xmlns="http://www.w3.org/2000/svg" version="1.1" ', attrs, '>', contents, '</svg>'])

svg o map = svgTemplate (concat ['style="width: ', (assocGetWithDefault 'width' 100 map), '; height: ' , (assocGetWithDefault 'height' 100 map), '"']) (o id)

svgFile map = html (concat [ '<object xmlns="http://www.w3.org/2000/svg" version="1.1" ', (insertFields map), '</object>' ] )

field cell = concat [ (key cell), '="', (value cell), '" ' ]

defaultCircleMap = assocFromList [ 'cx', 50, 'cy', 50, 'r', 40, 'stroke', 'black', 'stroke-width', 2, 'fill', 'red' ]
defaultRectMap = assocFromList [ 'x', 1, 'y', 1, 'width', 50, 'height', 50, 'stroke', 'black', 'stroke-width', 2, 'fill', 'green' ]
defaultLineMap = assocFromList [ 'x1', 0, 'y1', 0, 'x2', 50, 'y2', 50, 'stroke', 'black', 'stroke-width', 2 ]
defaultEllipseMap = assocFromList [ 'cx', 50, 'cy', 50, 'rx', 40, 'ry', 40, 'stroke', 'black', 'stroke-width', 2, 'fill', 'blue' ]
defaultTextMap = assocFromList [ 'x', 0, 'y', 10, 'stroke', 'black', 'stroke-width', 2, 'fill', 'black' ]
defaultPolygonMap = assocFromList [ 'points', '350,75  379,161 469,161 397,215 423,301 350,250 277,301 303,215 231,161 321,161', 'stroke', 'black', 'stroke-width', 2, 'fill', 'yellow' ]

svgNode contents = \f . f contents

svgNodes nodeList = nodeList (\h t D . cons (h id) (svgNodes t)) nil

svgConcat nodeList = svgNode (concat (svgNodes nodeList))

svgElement name map = svgNode (concat [ '<', name, ' ', (insertFields map), '/>' ])

svgMeasure content = primSvgMeasure content

svgPresent content = svgMeasure content \x y w h .
  svgTemplate (concat ["width='", w, "' height='", h, "' viewbox='", x, ' ', y, ' ', w, ' ', h, "'"]) content

circle map = svgElement 'circle' (if (null? map) defaultCircleMap map)
rect map = svgElement 'rect' (if (null? map) defaultRectMap map)
ellipse map = svgElement 'ellipse' (if (null? map) defaultEllipseMap map)
line map = svgElement 'line' (if (null? map) defaultLineMap map)
text t map = svgNode (concat [ '<text ', (insertFields (if (null? map) defaultTextMap map)), '>', (if (null? t) 'undefined' t), '</text>' ])
polygon map = svgElement 'polygon' (if (null? map) defaultPolygonMap map)

group elem = svgNode (concat [ '<g>', (elem id), '</g>' ])
translate elem x y = svgNode (concat ['<g transform="translate(', x, ', ', y, ')">', (elem id), '</g>' ])
rotate elem r = svgNode (concat ['<g transform="rotate(', r, ')">', (elem id), '</g>' ])

# bad compile doesnt delete old .js file
# can you print out html as plain text in the console

3
